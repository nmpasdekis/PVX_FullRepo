#version 440

layout(location = 0) in vec4 Position;
layout(location = 1) in vec3 Normal;


out Vert_t{
	vec4 Position;
	vec3 Normal;
	vec2 UV;
	vec4 Tangent;
} outVert;

struct MorphItem_t {
	vec4 Position;
	vec4 Normal;
};

layout(std140, binding = 0) uniform Camera{
	mat4 View;
	mat4 Projection;
	vec3 CameraPosition;
};

layout(std430, binding = 3) readonly buffer Transform {
	mat4 Model[];
};

layout(std430, binding = 4) readonly buffer MorphControl {
	float MorphWeight[];
};

layout(std430, binding = 5) readonly buffer MorphData {
	MorphItem_t[] MorphItem;
};

uniform int MorphCount;
uniform int BoneCount;

void main(){
	vec3 mPosition = Position.xyz;
	vec3 mNormal = Normal;

	int Item_Offset = gl_VertexID * MorphCount;
	int Control_Offset = gl_InstanceID * MorphCount;
	for (int i = 0; i < MorphCount; i++, Item_Offset++, Control_Offset++) {
		float w = MorphWeight[Control_Offset];
		MorphItem_t m = MorphItem[Item_Offset];
		mPosition += m.Position.xyz * w;
		mNormal += m.Normal.xyz * w;
	}
	vec4 MorphedPosition = vec4(mPosition, 1);
	vec3 MorphedNormal = normalize(mNormal);
	gl_Position = Projection * View * (outVert.Position = Model[gl_InstanceID] * MorphedPosition);
	outVert.Normal = mat3(Model[gl_InstanceID]) * MorphedNormal;
}